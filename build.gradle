// -*- Groovy -*-
// build.gradle
//
// Gradle build script for EC-EC2 plugin.
//
// Copyright (c) 2015 Electric Cloud, Inc.
// All rights reserved

buildscript {
	repositories {
		maven { url 'http://dl.bintray.com/ecpluginsdev/maven' }
		jcenter()
	}

	dependencies {
		classpath group: 'com.electriccloud.plugins', name: 'flow-gradle-plugin', version: '+'
	}
}

group = "com.electriccloud"
description = "Plugins : EC-EC2"
version = "2.4.1"

apply plugin: 'groovy'
apply plugin: 'flow-gradle-plugin'
apply plugin: 'license'

license {
	header = file ('shortHeader.txt')
	exclude "**/project.xml"
}

task wrapper(type: Wrapper) { gradleVersion = '2.13' }

// Configuration for the jar files we want
// to include in the plugin jar
configurations {
	includeInPluginJar
}

dependencies {
	compile localGroovy()
	compile 'com.electriccloud:commander-api:6.1.+'
	includeInPluginJar 'com.amazonaws:aws-java-sdk-ec2:1.9.33'
	includeInPluginJar 'com.amazonaws:aws-java-sdk-core:1.9.33-1'
	testCompile 'junit:junit:[4,)'
	testCompile 'org.mockito:mockito-core:1.9.5'
	testCompile 'com.amazonaws:aws-java-sdk-ec2:1.9.33'
}

test {
	Properties props = new Properties()

	systemProperties['COMMANDER_SERVER'] = "$commanderServer"
	systemProperties['PLUGIN_VERSION'] = version

	testLogging {
		// Show that tests are run in the command-line output
		events 'started', 'passed'
		exceptionFormat = 'full'
	}
}

// Include any dependent jars in the plugin jar file for
// evaluating the validation and dynamic option scripts
// on the ElectricFlow server.
jar {
	from(configurations.includeInPluginJar, { into('libs') })
}
